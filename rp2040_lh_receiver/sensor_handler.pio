.program level_monitor                ; sm0
.side_set 1

.wrap_target
level_compare:                        ; start of the level change detection loop
    mov isr, null side 0              ; clear isr
    in pins, 22 side 0                ; shift pins into isr
    mov x, isr side 0                 ; copy isr into x
    jmp x!=y light_trigger side 0     ; test if any pin has changed level
    jmp level_compare side 0          ; jump to next compare cycle
light_trigger:                        ; push and trigger sm1
    mov y, x side 0                   ; copy x into y
    push noblock side 0               ; push isr into rx fifo
    irq wait 4 side 1                 ; set irq and trigger wait for sm1 to process

;------------------------------------------------------------------------------------

.program time_stamp                   ; sm1

timer_start:
    mov x, ~null                      ; set x to 0xFFFFFFFF
timer_count:
    jmp x-- trigger_test              ; tick
    jmp timer_start                   ; reset timer
trigger_test:
    jmp pin, time_stamp_push          ; if triggered, push
    jmp timer_count                   ; else continue counting
time_stamp_push:
    mov isr, ~x                       ; bit invert
    push noblock
    irq clear 4